model	"BDF (font file) to C table converter"
{
	// constants
	macro	IFILE="shnmk14.bdf";		// 全角フォントファイル
	macro	IFILER="shnm7x14r.bdf";		// 半角フォントファイル


	// variables
	macro	dir,file,ext;
	pathsplit	$(pathname), dir,file,ext;


	// procedures
	procedure	kuten2sjis	codk,codt
	{
		// 区点コード(codk, codt) → S-JISコード
		// 引数のcodk，codtは1～94の区点コード。

		// 手続き内では1を減じて、0～93のコードとして変換する。
		codk=codk - 1;					// 区コード：0～93
		codt=codt - 1;					// 点コード：0～93

		kuten2sjis=0x8100 + 0x100*int (codk/2);
		if (codk≧62)
		{
			kuten2sjis=kuten2sjis + 0xe000 - 0xa000;
		}
		kuten2sjis=kuten2sjis + codt;
		if (int (codk%2)=0)
		{
			// odd: 1, 3, 5, ...
			if (codt≦62)
			{
				kuten2sjis=kuten2sjis + 0x40;
			}
			else
			{
				kuten2sjis=kuten2sjis + (0x80 - 63);
			}
		}
		else
		{
			// even: 2, 4, 6, ...
			kuten2sjis=kuten2sjis + 0x9f;
		}
	}
	procedure	fget_until	$$str
	{
		var		cmp;
		static : repeat
		{
			fgetl	fin,lbuf;
			if (status)
			{
				fget_until=-1;
				break;
			}
			strcmp	cmp=$(lbuf1),$(str);
		} until (cmp=0);
		mlet	fget_until=$(lbuf);
	}
	procedure	bdf2ctab
	{
		var		idx;
		var		cmp;
		var		num;
		var		err;
		var		codj;
		var		codk;
		var		codt;
		var		fpos;
		var		fsiz;
		macro	chr;
		macro	scod;

		// file size
		ftell	fin,fpos;
		fseek	fin,-1;
		ftell	fin,fsiz;
		fseek	fin,fpos;

		fget_until	"STARTFONT";
		err=err + fget_until;
		fget_until	"FONT";
		mlet	fnam=$(fget_until);
		fget_until	"PIXEL_SIZE";
		field	num=fget_until;
		err=err + fget_until;

		// loop
		fget_until	"ENCODING";
		err=err + fget_until;
		while (err=0)
		{
			ftell	fin,fpos;
			progress	"変換中…",100*fpos/fsiz;

			field	num=fget_until;
			codj=$(fget_until2);

			fget_until	"BITMAP";
			err=err + fget_until;

			if (codj<256)
			{
				// ASCII code
				num2str		scod=codj + 0x0300,"%04d";
			}
			else
			{
				// KU-TEN code
				codk=codj - 0x2020;
				codt=@(codk&0xff);
				codk=@((codk>>8)&0xff);
				num2str		scod=(codk - 1)*94 + codt - 1,"%04d";
			}
			fprint	fout,$(scod)":";

			idx=0;
			while (idx<14)
			{
				fgets	fin,lbuf;
				substr	lbuf=$(lbuf)"00",1,4;
				fprint	fout,"0x"$(lbuf)", ";

				// continue
				idx=idx + 1;
			}
			if (codj<256)
			{
				num2chr		chr=codj;
				sub		"\","￥",chr;
				sub		"#","##",chr;
			}
			else
			{
				kuten2sjis	codk,codt;
				num2chr		chr=kuten2sjis;
			}
			fprint	fout,"// "$(scod)" "$"$(chr)$"$,;

			// continue
			fget_until	"ENCODING";
			err=err + fget_until;
		}
	}


	// conversion
	var		fin;
	var		fout;
	macro	fnam;
	fopen	fout,$(file)"_out.txt","w";

	// 半角フォント
	fopen	fin,$(IFILER),"r";
	bdf2ctab;
	fclose	fin;

	// 全角フォント
	fopen	fin,$(IFILE),"r";
	bdf2ctab;
	fclose	fin;

	// 84区94点を追加
	fprint	fout,
			"7895:"
			"0xcccc, 0xcccc, 0x3330, 0x3330, "
			"0xcccc, 0xcccc, 0x3330, 0x3330, "
			"0xcccc, 0xcccc, 0x3330, 0x3330, "
			"0xcccc, 0xcccc, // 7895"$,;

	fclose	fout;


	// sort
	fsort	$(file)"_out.txt";


	// format
	var		idx;
	var		codkt;
	var		codpr;
	var		fpos;
	var		fsiz;

	fopen	fin,$(file)"_out.txt","r";
	fopen	fout,$(file)"_sjis.cpp","w";

	// file size
	ftell	fin,fpos;
	fseek	fin,-1;
	ftell	fin,fsiz;
	fseek	fin,fpos;

	// header
	fprint	fout,"//"$,;
	fprint	fout,"//	M5StickC Plus postscript library"$,;
	fprint	fout,"//		Japanese font module"$,;
	fprint	fout,"//		(c) 2022	1YEN Toru"$,;
	fprint	fout,"//"$,;
	fprint	fout,"//	Generated using script: "$(file)".olv"$,;
	fprint	fout,"//"$,;
	fprint	fout,$,;
	fprint	fout,$,;
	fprint	fout,"// "$(fnam)$,;
	fprint	fout,"// zenkaku kanji font 0~(84*94 - 1): "$(IFILE)$,;
	fprint	fout,"// hankaku ascii font 0x0300~0x03ff: "$(IFILER)$,;
	fprint	fout,"extern	const	unsigned	short	tft_prn_font_kz[]="$,;
	fprint	fout,"{"$,;

	codpr=0;
	fgets	fin,lbuf;
	while (!status)
	{
		ftell	fin,fpos;
		progress	"未定義領域…",100*fpos/fsiz;

		str2num		codkt=$(lbuf);
		idx=codpr + 1;
		while (idx<codkt)
		{
			if (idx=1128)
			{
				fprint	fout,
						"0x0780, 0x1860, 0x2010, 0x4488, "
						"0x4d54, 0x9d54, 0x8c00, 0x8c04, "
						"0x8c04, 0x4c08, 0x4008, 0x2010, "
						"0x1860, 0x0780, // 1128 "$""①"$"$,;
			}
			else
			{
				macro	tbuf;
				num2str		tbuf=idx,"%04d";
				fprint	fout,
						"0xcccc, 0xcccc, 0x3330, 0x3330, "
						"0xcccc, 0xcccc, 0x3330, 0x3330, "
						"0xcccc, 0xcccc, 0x3330, 0x3330, "
						"0xcccc, 0xcccc, // "$(tbuf)$,;
			}
			// continue
			idx=idx + 1;
		}
		codpr=codkt;

		psub	"^[0-9]+:","",lbuf;
		gsub	"#","##",lbuf;
		fprint	fout,$(lbuf)$,;

		// continue
		fgets	fin,lbuf;
	}
	fprint	fout,"};"$,;
	progress	"",0;

	fclose	fin;
	fclose	fout;


	// S-JIS → UTF-8変換
	// S-JISにはバックスラッシュ(\)が含まれているので、UTF-8に変換。
	ftxtcnv		$(file)"_sjis.cpp",$(file)".cpp", dst=csetUTF8;

//// if you need S-JIS file:
//fmove	$(dir)$/$(file)"_sjis.cpp",$(dir)$/$(file)".cpp", force;


	// delete temporary files
	fdel	$(file)"_out.txt";
	fdel	$(file)"_sjis.cpp";


	leave	stop_error;
}

